apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

description = 'Flux Chess Engine'

group = 'com.fluxchess'
version = '2.3.0-alpha.1'

if (!hasProperty('buildNumber')) {
  ext.buildNumber = 'dev'
}
if (version.contains('-')) {
  if (hasProperty('branchName') && branchName == 'master') {
    version += '.' + buildNumber
  } else {
    version += '.0'
  }
}

if (!hasProperty('revisionNumber')) {
  ext.revisionNumber = 'rev'
}
if (version.contains('-') && !(hasProperty('branchName') && branchName == 'master')) {
  version += '+' + revisionNumber
}

println "Building version ${version}"

repositories {
  mavenCentral()
  maven {
    url "http://maven.fluxchess.com/release"
  }
}

tasks.withType(Pmd) {
  pmd {
    ignoreFailures = true
  }
}

tasks.withType(FindBugs) {
  findbugs {
    ignoreFailures = true
  }
}

tasks.withType(Checkstyle) {
  checkstyle {
    ignoreFailures = true
    showViolations = false
  }
}

dependencies {
  compile 'com.fluxchess:jcpi:+'

  testCompile 'junit:junit:4.+'
  testCompile 'org.slf4j:slf4j-api:1.7.+'
  testCompile 'org.slf4j:slf4j-log4j12:1.7.+'
  testCompile 'log4j:log4j:1.2.+'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

processResources {
  filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
    version: project.version,
    buildNumber: project.buildNumber,
    revisionNumber: project.revisionNumber
  ])
}

jar {
  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }
  manifest {
    attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version, 'Main-Class': 'com.fluxchess.flux.Flux'
  }
}

task testsJar(type: Jar, dependsOn: testClasses) {
  classifier = 'tests'
  from sourceSets.test.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task dist(type: Zip) {
  def baseDir = "${project.name}-${project.version}"

  into("$baseDir") {
    from 'README.md'
    from 'COPYING'
    from 'COPYING.LESSER'

    from jar
    from testsJar
    from sourcesJar
    from javadocJar

    from ('src/dist/Flux.cmd') {
      filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
        version: project.version
      ])
    }
    from ('src/dist/Flux') {
      fileMode = 0755
      filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
        version: project.version
      ])
    }
  }
}

artifacts {
  archives testsJar
  archives sourcesJar
  archives javadocJar
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.8'
}
